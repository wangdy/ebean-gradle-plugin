group 'com.github.wangdy'
version '10.1.1'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.ebean:ebean-agent:10.1.1'
    compile 'io.ebean:querybean-agent:10.1.1'
    compile gradleApi()
    compile localGroovy()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'ebean-gradle-plugin'
                packaging 'jar'
                description 'Gradle plugin for enhancement Ebean entity beans'
                url 'https://github.com/wangdy/ebean-gradle-plugin'

                scm {
                    url 'scm:git@github.com/wangdy/ebean-gradle-plugin.git'
                    connection 'scm:git@github.com/wangdy/ebean-gradle-plugin.git'
                    developerConnection 'scm:git:https://github.com/wangdy/ebean-gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'wangdy'
                        name 'Wang Dongyang'
                    }
                }
            }
        }
    }
}